apply plugin: 'com.android.application'
apply plugin: 'com.github.ben-manes.versions'
apply plugin: 'com.github.triplet.play'

dependencies {
    implementation 'eu.chainfire:libsuperuser:1.0.0.201704021214'
    implementation 'com.android.support:design:27.0.2'
    implementation 'com.google.zxing:android-integration:3.3.0'
    implementation 'com.google.code.gson:gson:2.8.2'
    implementation 'org.mindrot:jbcrypt:0.4'
    implementation 'com.google.guava:guava:23.6-android'
    implementation 'com.annimon:stream:1.1.9'
    implementation 'com.android.volley:volley:1.1.0'
    implementation 'com.android.support.constraint:constraint-layout:1.0.2'
    implementation 'com.google.dagger:dagger:2.14.1'
    annotationProcessor "com.google.dagger:dagger-compiler:2.14.1"
    androidTestImplementation 'com.android.support.test:rules:1.0.1'
    androidTestImplementation 'com.android.support:support-annotations:27.0.2'
}

android {
    // Changes to these values need to be reflected in `.travis.yml`
    compileSdkVersion 27
    buildToolsVersion '27.0.3'

    buildTypes.debug.applicationIdSuffix ".debug"
    dataBinding.enabled = true

    playAccountConfigs {
        defaultAccountConfig {
            jsonFile = file(System.getenv("SYNCTHING_RELEASE_PLAY_ACCOUNT_CONFIG_FILE") ?: 'keys.json')
        }
    }
    defaultConfig {
        applicationId "com.nutomic.syncthingandroid"
        minSdkVersion 16
        targetSdkVersion 26
        versionCode 4147
        versionName "0.10.14-beta1"
        testApplicationId 'com.nutomic.syncthingandroid.test'
        testInstrumentationRunner 'android.support.test.runner.AndroidJUnitRunner'
        playAccountConfig = playAccountConfigs.defaultAccountConfig
    }

    signingConfigs {
        release {
            storeFile = {
                def path = System.getenv("SYNCTHING_RELEASE_STORE_FILE")
                return (path) ? file(path) : null
            }()
            storePassword System.getenv("SIGNING_PASSWORD") ?: ""
            keyAlias System.getenv("SYNCTHING_RELEASE_KEY_ALIAS") ?: ""
            keyPassword System.getenv("SIGNING_PASSWORD") ?: ""
        }
    }
    buildTypes {
        release {
            signingConfig = signingConfigs.release.storeFile ? signingConfigs.release : null
        }
        debug {
            debuggable true
            jniDebuggable true
            renderscriptDebuggable true
            minifyEnabled false
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

play {
    jsonFile = file(System.getenv("SYNCTHING_RELEASE_PLAY_ACCOUNT_CONFIG_FILE") ?: 'keys.json')
    uploadImages = false
    track = 'beta'
}

/**
 * Some languages are not supported by Google Play, so we ignore them.
 */
task deleteUnsupportedPlayTranslations(type: Delete) {
    delete 'src/main/play/nn'
    delete 'src/main/play/el-EL'
    delete 'src/main/play/nb'
    delete 'src/main/play/en/'
}

plugins {
    id 'com.android.application'
    id 'com.github.ben-manes.versions'
    id 'com.github.triplet.play' version '2.8.0'
}

dependencies {
    implementation 'eu.chainfire:libsuperuser:1.1.0.202004101746'
    implementation 'com.google.android.material:material:1.0.0'
    implementation 'com.google.code.gson:gson:2.8.7'
    implementation 'org.mindrot:jbcrypt:0.4'
    implementation 'com.google.guava:guava:30.1.1-android'
    implementation 'com.annimon:stream:1.2.2'
    implementation 'com.android.volley:volley:1.2.0'

    implementation ('com.journeyapps:zxing-android-embedded:4.1.0') { transitive = false }
    implementation 'com.google.zxing:core:3.3.0'

    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'com.google.dagger:dagger:2.37'
    annotationProcessor 'com.google.dagger:dagger-compiler:2.36'
    androidTestImplementation 'androidx.test:rules:1.3.0'
    androidTestImplementation 'androidx.annotation:annotation:1.1.0'
}

android {
    // Changes to these values need to be reflected in `.travis.yml`
    compileSdkVersion 29
    buildToolsVersion '29.0.3'
    ndkVersion = '21.4.7075529'

    buildTypes.debug.applicationIdSuffix ".debug"
    dataBinding.enabled = true

    playConfigs {
        defaultAccountConfig {
            serviceAccountCredentials = file(System.getenv("SYNCTHING_RELEASE_PLAY_ACCOUNT_CONFIG_FILE") ?: 'keys.json')
        }
    }
    defaultConfig {
        applicationId "com.nutomic.syncthingandroid"
        minSdkVersion 16
        targetSdkVersion 29
        versionCode 4279
        versionName "1.18.0"
        testApplicationId 'com.nutomic.syncthingandroid.test'
        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
    }

    signingConfigs {
        release {
            storeFile = {
                def path = System.getenv("SYNCTHING_RELEASE_STORE_FILE")
                return (path) ? file(path) : null
            }()
            storePassword System.getenv("SIGNING_PASSWORD") ?: ""
            keyAlias System.getenv("SYNCTHING_RELEASE_KEY_ALIAS") ?: ""
            keyPassword System.getenv("SIGNING_PASSWORD") ?: ""
        }
    }
    buildTypes {
        release {
            signingConfig = signingConfigs.release.storeFile ? signingConfigs.release : null
        }
        debug {
            debuggable true
            jniDebuggable true
            renderscriptDebuggable true
            minifyEnabled false
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

play {
    serviceAccountCredentials = file(System.getenv("SYNCTHING_RELEASE_PLAY_ACCOUNT_CONFIG_FILE") ?: 'keys.json')
    track = 'beta'
}

/**
 * Some languages are not supported by Google Play, so we ignore them.
 */
task deleteUnsupportedPlayTranslations(type: Delete) {
    delete 'src/main/play/listings/el-EL/'
    delete 'src/main/play/listings/en/'
    delete 'src/main/play/listings/eu/'
    delete 'src/main/play/listings/nb/'
    delete 'src/main/play/listings/nl_BE/'
    delete 'src/main/play/listings/nn/'
    delete 'src/main/play/listings/ta/'
}
